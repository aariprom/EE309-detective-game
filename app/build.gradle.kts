import java.util.Properties

plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    id("com.google.dagger.hilt.android")
    id("org.jetbrains.kotlin.plugin.serialization")
    id("com.google.devtools.ksp")
    // ì»¤ë²„ë¦¬ì§€ëŠ” ë‚´ì¥ ì˜µì…˜ ëŒ€ì‹  ìˆœì • jacoco ì‚¬ìš©
    id("jacoco")
    id("org.jetbrains.kotlin.plugin.compose")
}

// Load local.properties
val localProperties = Properties()
val localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localProperties.load(localPropertiesFile.inputStream())
}

fun loadEnv(name: String): String? =
    localProperties.getProperty(name) ?: System.getenv(name)

jacoco {
    toolVersion = "0.8.12"
}

kotlin {
    compilerOptions {
        jvmTarget.set(
            org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17
        )
    }
}

android {
    namespace = "com.ee309.detectivegame"
    compileSdk = 36

    defaultConfig {
        applicationId = "com.ee309.detectivegame"
        minSdk = 24
        targetSdk = 36
        versionCode = 2
        versionName = "1.1"

        // testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        // override above by hilt test runner
        testInstrumentationRunner = "com.ee309.detectivegame.HiltTestRunner"
        vectorDrawables { useSupportLibrary = true }

        val upstageApiKey = loadEnv("UPSTAGE_API_KEY") ?: ""
        val upstageBaseUrl = loadEnv("UPSTAGE_BASE_URL") ?: ""

        buildConfigField("String", "UPSTAGE_API_KEY", localProperties.getProperty("UPSTAGE_API_KEY", "\"${upstageApiKey}\""))
        buildConfigField("String", "UPSTAGE_BASE_URL", localProperties.getProperty("UPSTAGE_BASE_URL", "\"${upstageBaseUrl}\""))
    }

    buildTypes {
        release {
            isMinifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
        debug {
            // ğŸ”´ ë¬¸ì œì˜€ë˜ ë‚´ì¥ ìœ ë‹› í…ŒìŠ¤íŠ¸ ì»¤ë²„ë¦¬ì§€ëŠ” ë¹„í™œì„±í™”
            // enableUnitTestCoverage = true
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    buildFeatures { 
        compose = true 
        buildConfig = true
    }
    
    // marked unstable to use with @Incubating, suppress for now
    @Suppress("UnstableApiUsage")
    composeOptions { kotlinCompilerExtensionVersion = "1.5.8" }

    packaging {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
}

dependencies {
    // Core Android
    implementation("androidx.core:core-ktx:1.17.0")
    implementation("androidx.lifecycle:lifecycle-runtime-ktx:2.10.0")
    implementation("androidx.activity:activity-compose:1.12.0")

    // Compose
    implementation(platform("androidx.compose:compose-bom:2025.11.01"))
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.ui:ui-graphics")
    implementation("androidx.compose.ui:ui-tooling-preview")
    implementation("androidx.compose.material3:material3")
    implementation("androidx.compose.material:material-icons-extended:1.7.8")

    // Compose Navigation
    implementation("androidx.navigation:navigation-compose:2.9.6")

    // ViewModel
    implementation("androidx.lifecycle:lifecycle-viewmodel-compose:2.10.0")
    implementation("androidx.lifecycle:lifecycle-runtime-compose:2.10.0")
    implementation("androidx.lifecycle:lifecycle-viewmodel-savedstate:2.10.0")

    // Hilt
    implementation("com.google.dagger:hilt-android:2.57.2")
    implementation("androidx.test:runner:1.7.0")
    ksp("com.google.dagger:hilt-android-compiler:2.57.2")
    implementation("androidx.hilt:hilt-navigation-compose:1.3.0")

    // Retrofit
    implementation("com.squareup.retrofit2:retrofit:3.0.0")
    implementation("com.squareup.retrofit2:converter-gson:3.0.0")
    implementation("com.squareup.okhttp3:okhttp:5.3.2")
    implementation("com.squareup.okhttp3:logging-interceptor:5.3.2")

    // Room
    implementation("androidx.room:room-runtime:2.8.4")
    implementation("androidx.room:room-ktx:2.8.4")
    ksp("androidx.room:room-compiler:2.8.4")

    // Kotlin Serialization
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0")

    // Coroutines
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2")

    // Testing
    testImplementation("junit:junit:4.13.2")
    androidTestImplementation("androidx.test.ext:junit:1.1.5")
    androidTestImplementation("androidx.test.espresso:espresso-core:3.7.0")
    androidTestImplementation(platform("androidx.compose:compose-bom:2025.11.01"))
    androidTestImplementation("androidx.compose.ui:ui-test-junit4")
    androidTestImplementation("com.google.dagger:hilt-android-testing:2.57.2")
    debugImplementation("androidx.compose.ui:ui-tooling")
    debugImplementation("androidx.compose.ui:ui-test-manifest")
    kspAndroidTest("com.google.dagger:hilt-android-compiler:2.57.2")
}

/* â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
   Jacoco ì„¤ì • (ìœ ë‹› í…ŒìŠ¤íŠ¸ìš©)
   - í…ŒìŠ¤íŠ¸ taskì— ì—ì´ì „íŠ¸ í™•ì‹¤íˆ ë¶€ì°©
   - exec íŒŒì¼ì„ í‘œì¤€/ëŒ€ì²´ ê²½ë¡œ íŒ¨í„´ìœ¼ë¡œ ì•ˆì „í•˜ê²Œ ìˆ˜ì§‘
   - Provider/Lazy ë°©ì‹ìœ¼ë¡œ ì˜ì¡´ë§Œ ì„ ì–¸(ì¡°ê¸° ì ‘ê·¼ ë°©ì§€)
   â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ */

tasks.withType<Test>().configureEach {
    // í…ŒìŠ¤íŠ¸ ì™„ë£Œ í›„ ìë™ìœ¼ë¡œ ë¦¬í¬íŠ¸ ìƒì„±
    finalizedBy("jacocoDebugUnitTestReport")

    // ì¼ë¶€ í™˜ê²½ì—ì„œ ì»¤ë²„ë¦¬ì§€ ìˆ˜ì§‘ ëˆ„ë½ ë°©ì§€
    extensions.configure(JacocoTaskExtension::class) {
        isIncludeNoLocationClasses = true
        excludes = listOf("jdk.internal.*")
    }
}

tasks.register<JacocoReport>("jacocoDebugUnitTestReport") {
    dependsOn("testDebugUnitTest")

    // ì‹¤í–‰ ë°ì´í„°(.exec) ìˆ˜ì§‘: í‘œì¤€ + ì˜ˆì™¸ ê²½ë¡œë“¤
    val execFiles = fileTree(layout.buildDirectory.asFile.get()) {
        include(
            "jacoco/testDebugUnitTest.exec",          // í‘œì¤€
            "jacoco/test.exec",                       // ê²½ìš°ì— ë”°ë¼ ìƒì„±ë  ìˆ˜ ìˆìŒ
            "outputs/unit_test_code_coverage/**.exec",
            "**/jacoco-ut/*.exec"
        )
    }
    executionData( execFiles)

    // í´ë˜ìŠ¤/ì†ŒìŠ¤ ë””ë ‰í† ë¦¬
    val kotlinClasses = fileTree(layout.buildDirectory.dir("tmp/kotlin-classes/debug"))
    val javaClasses = fileTree(layout.buildDirectory.dir("intermediates/javac/debug/classes"))

    val excludes = listOf(
        "**/R.class", "**/R$*.class", "**/BuildConfig.*", "**/Manifest*.*",
        "androidx/**", "**/*Test*.*", "**/*$*Companion*.*", "**/*$*WhenMappings*.*",
        // Hilt/DI ìƒì„±ë¬¼
        "**/*_Factory.*", "**/*_Hilt*.*", "**/*_MembersInjector.*",
        "**/*_Provide*Factory.*", "**/*Hilt*.*"
    )

    classDirectories.setFrom(
        files(
            kotlinClasses.apply { exclude(excludes) },
            javaClasses.apply { exclude(excludes) }
        )
    )
    sourceDirectories.setFrom(files("src/main/java", "src/main/kotlin"))

    reports {
        xml.required.set(true)
        html.required.set(true)
        csv.required.set(false)
        html.outputLocation.set(layout.buildDirectory.dir("reports/jacoco/jacocoDebugUnitTestReport/html"))
        xml.outputLocation.set(layout.buildDirectory.file("reports/jacoco/jacocoDebugUnitTestReport/report.xml"))
    }
}
